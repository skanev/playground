(define (make-stack)
  (let ((s '())
       (number-pushes 0)
       (max-depth 0)
       (current-depth 0))
    (define (push x)
      (set! s (cons x s))
      (set! number-pushes (+ 1 number-pushes))
      (set! current-depth (+ 1 current-depth))
      (set! max-depth (max current-depth max-depth)))
    (define (pop)
      (if (null? s)
          (error "Empty stack -- POP")
          (let ((top (car s)))
            (set! s (cdr s))
            (set! current-depth (- current-depth 1))
            top)))
    (define (initialize)
      (set! s '())
      (set! number-pushes 0)
      (set! max-depth 0)
      (set! current-depth 0)
      'done)
    (define (statistics)
      (list 'total-pushes '= number-pushes
            'maximum-depth '= max-depth))
    (define (dispatch message)
      (cond ((eq? message 'push) push)
            ((eq? message 'pop) (pop))
            ((eq? message 'initialize) (initialize))
            ((eq? message 'statistics) (statistics))
            (else (error "Unknown request -- STACK" message))))
    dispatch))

(define (stack-stats-for machine exp)
  (set-register-contents! machine 'exp exp)
  ((machine 'stack) 'initialize)
  (start machine)
  ((machine 'stack) 'statistics))
